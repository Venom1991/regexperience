<start>                            ::= <anchored-expression> "EOI"
<anchored-expression>              ::= <start-anchor> <expression> <end-anchor>
<start-anchor>                     ::= "^"
                                    |  ε
<end-anchor>                       ::= "$"
                                    |  ε
<expression>                       ::= <simple-expression> <expression-prime>
<expression-prime>                 ::= <alternation>
                                    |  ε
<alternation>                      ::= "|" <simple-expression> <alternation-prime>
<alternation-prime>                ::= <alternation>
                                    |  ε
<simple-expression>                ::= <basic-expression> <simple-expression-prime>
<simple-expression-prime>          ::= <concatenation>
                                    |  ε
<concatenation>                    ::= <basic-expression> <concatenation-prime>
<concatenation-prime>              ::= <concatenation>
                                    |  ε
<basic-expression>                 ::= <elementary-expression> <basic-expression-prime>
<basic-expression-prime>           ::= <star-quantification>
                                    |  <plus-quantification>
                                    |  <question-mark-quantification>
                                    |  ε
<star-quantification>              ::= "*"
<plus-quantification>              ::= "+"
<question-mark-quantification>     ::= "?"
<elementary-expression>            ::= <group>
                                    |  <bracket-expression>
                                    |  <empty-expression>
                                    |  <upper-case-letter>
                                    |  <lower-case-letter>
                                    |  <digit>
                                    |  <special-character>
                                    |  <bracket-expression-metacharacter>
                                    |  <any-character>
                                    |  "\" <elementary-expression-prime>
<elementary-expression-prime>      ::= <regular-metacharacter>
                                    |  <metacharacter-escape>
<group>                            ::= "(" <anchored-expression> ")"
<bracket-expression>               ::= "[" <bracket-expression-items> "]"
<bracket-expression-items>         ::= <bracket-expression-item> <bracket-expression-items-prime>
<bracket-expression-items-prime>   ::= <bracket-expression-items>
                                    |  ε
<bracket-expression-item>          ::= <upper-case-letter> <upper-case-letter-range>
                                    |  <lower-case-letter> <lower-case-letter-range>
                                    |  <digit> <digit-range>
                                    |  <special-character>
                                    |  <regular-metacharacter>
                                    |  "\" <bracket-expression-item-prime>
<bracket-expression-item-prime>    ::= <bracket-expression-metacharacter>
                                    |  <metacharacter-escape>
<upper-case-letter-range>          ::= "-" <upper-case-letter>
                                    |  ε
<lower-case-letter-range>          ::= "-" <lower-case-letter>
                                    |  ε
<digit-range>                      ::= "-" <digit>
                                    |  ε
<upper-case-letter>                ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                                    |  "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<lower-case-letter>                ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                                    |  "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit>                            ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<special-character>                ::= "!" | "#" | "%" | "&" | "," | "/" | ":" | ";" | ">" | "=" | "<" | "@" | "_"
                                    |  "`" | "{" | "}" | "~" | " " | "\n" | "\t"
<regular-metacharacter>            ::= "^" | "$" | "[" | "(" | ")" | "*" | "+" | "?" | "|" | "."
<bracket-expression-metacharacter> ::= "-" | "]"
<any-character>                    ::= "."
<metacharacter-escape>             ::= "\"
<empty-expression>                 ::= ""
