<start>                            ::= <expression> "EOI"
<expression>                       ::= <simple-expression> <alternation>
                                    |  <simple-expression>
<alternation>                      ::= "|" <simple-expression> <alternation>
                                    |  "|" <simple-expression>
<simple-expression>                ::= <basic-expression> <concatenation>
                                    |  <basic-expression>
<concatenation>                    ::= <basic-expression> <concatenation>
                                    |  <basic-expression>
<basic-expression>                 ::= <star-quantification>
                                    |  <plus-quantification>
                                    |  <question-mark-quantification>
                                    |  <elementary-expression>
<star-quantification>              ::= <elementary-expression> "*"
<plus-quantification>              ::= <elementary-expression> "+"
<question-mark-quantification>     ::= <elementary-expression> "?"
<elementary-expression>            ::= <group>
                                    |  <bracket-expression>
                                    |  <upper-case-letter>
                                    |  <lower-case-letter>
                                    |  <digit>
                                    |  <special-character>
                                    |  <bracket-expression-metacharacter>
                                    |  "\" <regular-metacharacter>
                                    |  "\" <metacharacter-escape>
<group>                            ::= "(" <expression> ")"
<bracket-expression>               ::= "[" <bracket-expression-items> "]"
<upper-case-letter>                ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                                    |  "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<lower-case-letter>                ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                                    |  "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit>                            ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<special-character>                ::= "!" | "#" | "$" | "%" | "&" | "," | "." | "/" | ":" | ";" | ">" | "=" | "<"
                                    |  "@" | "^" | "_" | "`" | "{" | "}" | "~" | " " | "\n" | "\t"
<regular-metacharacter>            ::= "[" | "(" | ")" | "*" | "+" | "?" | "|"
<bracket-expression-metacharacter> ::= "-" | "]"
<metacharacter-escape>             ::= "\"
<bracket-expression-items>         ::= <bracket-expression-item>
                                    |  <bracket-expression-item> <bracket-expression-items>
<bracket-expression-item>          ::= <range>
                                    |  <upper-case-letter>
                                    |  <lower-case-letter>
                                    |  <digit>
                                    |  <special-character>
                                    |  <regular-metacharacter>
                                    |  "\" <bracket-expression-metacharacter>
                                    |  "\" <metacharacter-escape>
<range>                            ::= <upper-case-letter> "-" <upper-case-letter>
                                    |  <lower-case-letter> "-" <lower-case-letter>
                                    |  <digit> "-" <digit>
